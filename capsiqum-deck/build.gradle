/*
 * Copyright 2023-2024 wcaokaze
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
   alias libs.plugins.kotlin.multiplatform
   alias libs.plugins.android.library
   alias libs.plugins.compose.jb
   alias libs.plugins.compose.compiler
   alias libs.plugins.roborazzi
}

kotlin {
   androidTarget()
   jvm('desktop')

   compilerOptions {
      freeCompilerArgs = ['-Xexpect-actual-classes']
   }

   jvmToolchain(17)

   applyDefaultHierarchyTemplate()

   sourceSets {
      commonMain.dependencies {
         implementation libs.kotlinx.collections.immutable
         implementation compose.foundation
      }

      commonTest.dependencies {
         implementation compose.material3
         implementation compose.uiTestJUnit4
         implementation libs.kotlin.test.junit
         implementation libs.roborazzi.core
         implementation libs.roborazzi.compose
      }

      jvmAndroidMain {
         dependsOn(commonMain)

         dependencies {
         }
      }

      jvmAndroidTest {
         dependsOn(commonTest)

         dependencies {
            implementation libs.mockk
         }
      }

      androidMain {
         dependsOn(jvmAndroidMain)

         dependencies {
            implementation compose.uiTooling
            implementation compose.preview
         }
      }

      androidUnitTest {
         dependsOn(jvmAndroidTest)

         dependencies {
            implementation libs.androidx.test.ext
            implementation libs.robolectric
            implementation libs.compose.ui.test.manifest
         }
      }

      desktopMain {
         dependsOn(jvmAndroidMain)

         dependencies {
            implementation compose.uiTooling
            implementation compose.preview
         }
      }

      desktopTest {
         dependsOn(jvmAndroidTest)

         dependencies {
            implementation compose.desktop.currentOs
         }
      }
   }
}

android {
   namespace 'com.wcaokaze.probosqis.capsiqum.deck'

   compileSdk 34

   defaultConfig {
      minSdk 21
      targetSdk 34
   }

   buildFeatures {
      compose true
   }

   compileOptions {
      coreLibraryDesugaringEnabled true
      sourceCompatibility = '1.17'
      targetCompatibility = '1.17'
   }

   composeOptions {
      kotlinCompilerExtensionVersion = libs.versions.compose.compiler.get()
   }

   testOptions {
      unitTests {
         includeAndroidResources = true
      }
   }
}

dependencies {
   coreLibraryDesugaring libs.android.desugaring.jdk
}

compose {
   kotlinCompilerPlugin.set("androidx.compose.compiler:compiler:${libs.versions.compose.compiler.get()}")
}
